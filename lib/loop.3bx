# 3BX Standard Library - Loop Patterns
# This module provides common loop constructs using the pattern system.
# All loops use the loop_while intrinsic for efficiency - they compile
# directly to LLVM loops without recursive pattern calls.

import section.3bx

# =============================================================================
# While Loop
# =============================================================================
# Basic while loop with a lazy condition.
# The condition is re-evaluated on each iteration.
#
# Usage:
#   loop while x < 10:
#       print x
#       set x to x + 1
#
section [|loop] while {expression:condition}:
    execute:
        @intrinsic("loop_while", condition, the caller's child section)

# =============================================================================
# Repeat N Times
# =============================================================================
# Loop a fixed number of times.
# The variable 'loopindex' is available in the loop body (0-indexed).
#
# Usage:
#   loop 5 times:
#       print "Hello"
#
section loop count times:
    execute:
        set loopindex to 0
        loop while loopindex < count:
            execute the caller's child section
            set loopindex to loopindex + 1

# =============================================================================
# Do-While Loop
# =============================================================================
# Executes the body at least once, then continues while condition is true.
#
# Usage:
#   set x to 0
#   do then loop while x < 5:
#       print x
#       set x to x + 1
#
section do then loop while {expression:condition}:
    execute:
        execute the caller's child section
		loop while evaluate condition:
			execute the caller's child section

# =============================================================================
# Range Loop (Exclusive)
# =============================================================================
# Loop from start to end (exclusive). The variable 'loopindex' contains
# the current value.
#
# Usage:
#   loop from 1 to 10:
#       print loopindex
#
section loop from startval to endval [with step stepval|]:
    execute:
		if stepval isn't set:
			set stepval to 1
        set loopindex to startval
        loop while loopindex < endval:
            execute the caller's child section
            set loopindex to loopindex + stepval

# =============================================================================
# Range Loop (Inclusive)
# =============================================================================
# Loop from start to end (inclusive). The variable 'loopindex' contains
# the current value.
#
# Usage:
#   loop from 1 through 10:
#       print loopindex
#
section loop from startval through endval:
    execute:
        set loopindex to startval
        set limit to endval
        loop while loopindex <= limit:
            execute the caller's child section
            set loopindex to loopindex + 1

# =============================================================================
# Countdown Loop
# =============================================================================
# Loop counting down from a value to 1.
# The variable 'loopindex' contains the current value.
#
# Usage:
#   countdown from 10:
#       print loopindex
#       # prints 10, 9, 8, ..., 1
#
section countdown from startval:
    execute:
        set loopindex to startval
        loop while loopindex > 0:
            execute the caller's child section
            set loopindex to loopindex - 1

# =============================================================================
# Loop Until
# =============================================================================
# Loop until a condition becomes true (opposite of while).
# Uses execute_if to check the negated condition.
#
# Usage:
#   set x to 0
#   loop until x == 5:
#       print x
#       set x to x + 1
#
section loop until {expression:condition}:
    execute:
		loop while evaluate condition isn't true:
			execute the caller's child section

# =============================================================================
# Forever Loop
# =============================================================================
# Infinite loop - use with caution! Must contain a break mechanism.
# This is primarily for demonstration; use loop while true for infinite loops.
#
# Usage:
#   set x to 0
#   loop forever:
#       set x to x + 1
#       if x > 100 then:
#           # Need a way to break
#
section loop forever:
    execute:
        loop while true:
			execute the caller's child section
